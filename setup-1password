#!/usr/bin/env zsh

(( ! ${+commands[op]} )) && { echo "1password CLI does not exist"; exit 1; }

agent_config=$XDG_CONFIG_HOME/1Password/ssh/agent.toml
if [[ -f $agent_config ]]; then
    echo "1Password agent configuration file already exists!\n"
    cat $agent_config
    echo "\nDo you want to overwrite it? [y/N]"
    read -q "REPLY?Overwrite? "
    if [[ ! $REPLY =~ ^[Yy]$ ]]; then
        echo "Aborting..."
        exit 1
    fi
    rm $agent_config
fi

keys=$(op item list --categories "SSH Key" --format json)

selected_items=( $(jq -r '.[].title' <<<$keys | fzf --multi) )

for item in $selected_items; do
    vault=$(jq -r ".[] | select(.title==\"$item\") | .vault.name" <<<$keys)

    cat <<EOF >> $agent_config
[[ssh-keys]]
item = "$item"
vault = "$vault"

EOF
done
